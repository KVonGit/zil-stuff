"Father's Day - MAIN FILE"

<VERSION XZIP>
<CONSTANT RELEASEID 1>

<CONSTANT GAME-BANNER
"Father's Day
|An Interactive Tale of Terror
|By Alan Smithee">

<ROUTINE GO ()
  <SETG MODE ,VERBOSE>
  <SETG HERE ,CEMETERY>
  <MOVE ,PLAYER ,HERE>
  <CRLF>
  <CRLF>
  <ITALICIZE "You visit your dad every Father's Day...">
  <CRLF>
  <CRLF>
  <V-VERSION>
  <CRLF>
  <V-LOOK>
  <MAIN-LOOP>>
  
;"----------------------- UNCOMMENT TO ENABLE DEBUGGING COMMANDS ----------------------------------"
;"<COMPILATION-FLAG DEBUGGING-VERBS T>"

;"----------------------- UNCOMMENT TO ADD FLAGS --------------------------------------------------"
<SETG EXTRA-FLAGS (VEHBIT)>


<INSERT-FILE "parser">

;"------------------------- UNCOMMENT TO REDEFINE STUFF -------------------------------------------"
<SET REDEFINE T>


;"==================================== GAME WORLD ================================================="

<ROOM CEMETERY
    (DESC "Cemetery")
    (IN ROOMS)
    (LDESC "Your family cemetery has seen better days, but no one here ever complains.||The woods
surround you, except to the northwest (towards the front yard).")
    (EAST TO WOODS-TWO)
    (WEST TO WOODS)
    (NORTHEAST TO WOODS-THREE)
    (NORTHWEST TO FRONT-YARD)
    (FLAGS LIGHTBIT)
    (ACTION CEMETERY-R)>

<ROUTINE CEMETERY-R (RARG)
  <COND
    (<==? .RARG ,M-BEG>
      <COND
        (<AND <VERB? WALK> <==? ,ZOMBIE-HAND-GOT-YA 1>>
          <TELL "You can't go anywhere! The hand is holding onto you and you can't walk!!!" CR>
          <SETG P-CONT -1>)>)>>


<OBJECT YOUR-DADS-GRAVE
    (IN CEMETERY)
    (DESC "your father's grave")
    (FDESC "You stand at your father's grave.")
    (LDESC "Not much to it, besides the headstone.")
    (SYNONYM GRAVE)
    (ADJECTIVE YOUR DADS FATHER DAD\'S FATHER\'S)
    (FLAGS NARTICLEBIT)>

<OBJECT HEADSTONE
  (IN CEMETERY)
  (DESC "headstone")
  (TEXT "IF YOU CAN READ THIS, THE GAME IS BROKEN.")
  (SYNONYM TOMBSTONE HEADSTONE STONE HEADST WRITIN WRITING WORDS)
  (ADJECTIVE HEAD)
  (FLAGS READBIT NDESCBIT)
  (ACTION HEADSTONE-R)>


<OBJECT ZOMBIE-HAND
  (IN YOUR-DADS-GRAVE)
  (DESC "zombie hand")
  (SYNONYM HAND)
  (ADJECTIVE ZOMBIE)
  (ACTION ZOMBIE-HAND-R)
  (FLAGS NDESCBIT)>


<GLOBAL ZOMBIE-BITE-COUNTDOWN 4>
<GLOBAL ZOMBIE-EXITS <>>
<GLOBAL ZOMBIE-HAND-GOT-YA <>>
<GLOBAL ONCE-BITTEN <>>
<GLOBAL TWICE-SHY <>>

<ROUTINE HEADSTONE-R ()
  <COND
    (<VERB? READ>
      <COND
        (<==? <LOC ,ZOMBIE-HAND> ,YOUR-DADS-GRAVE>
          <TELL
"Just as you bend down to read it, a HAND bursts out of the ground and grabs you!!!" CR>
          <MOVE ,ZOMBIE-HAND ,HERE>
          <SETG ZOMBIE-HAND-GOT-YA 1>
          <QUEUE I-ZOMBIE-HAND -1>)
        (T
          <TELL
"There's no time for reading that right now!" CR>
          <RTRUE>)>)
    (<VERB? EXAMINE>
      <TELL 
"The writing on it is so small, you've never even tried to read it." CR>)>>
          
<ROUTINE ZOMBIE-EMERGES-R ()
  <TELL 
"A hideous ZOMBIE crawls out of your dad's grave!" CR>
  <MOVE ,ZOMBIE ,HERE>
  <QUEUE I-ZOMBIE -1>>


<ROUTINE I-ZOMBIE ()
  <COND
    (<==? <LOC ,ZOMBIE> ,HERE>
      <SETG ZOMBIE-BITE-COUNTDOWN <- ,ZOMBIE-BITE-COUNTDOWN 1>>
      <COND
        (<G? ,ZOMBIE-BITE-COUNTDOWN 1>
          <TELL
"||The zombie slowly limps towards you..." CR>)
        (<==? ,ZOMBIE-BITE-COUNTDOWN 1>
          <TELL
"||The zombie is VERY close!!!" CR>)
        (T
          <ZOMBIE-BITE-R>)>)
    (T
      <TEST-ZOMBIE-EXITS <LOC ,ZOMBIE>>)>>

<ROUTINE TEST-ZOMBIE-EXITS (ZLOC "AUX" TABLE IDX CHOSEN DEST)
 <SET TABLE <ITABLE 10>>
 <SET IDX 0>
 <MAP-DIRECTIONS (D P .ZLOC)
   <PUTB .TABLE .IDX <GET .P ,REXIT>>
   <SET IDX <+ .IDX 1>>>
 <COND (<0? .IDX>
         ;<TELL "No exits for the ZOMBIE!" CR>)
        (T
         <SET CHOSEN <RANDOM .IDX>>
         <SET DEST <GETB .TABLE .CHOSEN>>
         ;<TELL "The ZOMBIE randomly moves to " D .DEST "." CR>
         <MOVE ,ZOMBIE .DEST>)>>

<ROUTINE ZOMBIE-BITE-R ()
  <COND
    (<NOT <==? ,ONCE-BITTEN 1>>
      <SETG ONCE-BITTEN 1>
      <TELL "TODO: The zombie BITES YOU!!!" CR>)
    (T
      <TELL "TODO: The zombie stands silently beside you." CR>)>>


<ROUTINE ZOMBIE-HAND-R ()
  <COND
    (<VERB? ATTACK>
      <COND
        (<==? ,ZOMBIE-HAND-GOT-YA 1>
          <SETG ZOMBIE-HAND-GOT-YA <>>
          <TELL 
"After a brief struggle with the zombie hand, you eventually manage to get it off of you." CR>)
        (T
          <TELL "Attacking the hand has no obvious effect. It just flops back and forth." CR>)>)
    (<VERB? EXAMINE>
      <TELL "It looks just like every other zombie hand you've ever seen." CR>)
    (<VERB? SPEAK>
      <TELL "The zombie hand snaps back at you." CR>)
    (<VERB? TAKE>
      <COND
        (<==? ,ZOMBIE-HAND-GOT-YA 1>
          <TELL "You have lost your mind. It's got a hold of YOU, not the other way around!" CR>)
        (T
          <COND
            (<NOT <FSET? ,ZOMBIE-HAND ,TOUCHBIT>>
              <FSET ,ZOMBIE-HAND ,TOUCHBIT>
              <TELL
"You get a good, firm hold on the hand and... it breaks off just below the wrist!!! It quickly runs
up your arm and perches, palm-down, upon your shoulder." CR>
              <MOVE ,ZOMBIE-HAND ,PLAYER>)
            (T
              <TELL 
"After thumb-wrestling with the stupid hand for half a turn, you finally manage to pick it up. It
lets its fingers do the walking up your arm and perches, palm-down, upon your shoulder." CR>
          <MOVE ,ZOMBIE-HAND ,PLAYER>)>)>)>>

<ROUTINE I-ZOMBIE-HAND ()
  <COND
    (<AND
      <IN? ,ZOMBIE-HAND ,PLAYER>
      <NOT
        <==? ,PRSO ,ZOMBIE-HAND>>>
          <TELL CR "The zombie hand rests happily on your shoulder." CR>)
    (<AND
      <==? <LOC ,ZOMBIE-HAND> ,HERE>
      <NOT
        <PRSO? ,ZOMBIE-HAND>>>
          <COND
            (<==? ,ZOMBIE-HAND-GOT-YA 1>
              <TELL CR "The zombie hand has a firm grip on you!" CR>)
            (T
              <COND
                (<FSET? ,ZOMBIE-HAND ,TOUCHBIT>
                  <TELL "The zombie hand flops around helplessly." CR>)
                (T
                  <TELL CR "The zombie hand flails around aimlessly." CR>)>)>)>>

;" TODO: Create HAND-THING to replace the hand once taken."
;"        Also fix the inventory with the pocket."
;"         Also, QUEUE I-ZOMBIE when you take the BEER."

<OBJECT ZOMBIE
  (IN YOUR-DADS-GRAVE)
  (DESC "your dad")
  (SYNONYM DAD FATHER ZOMBIE)
  (ADJECTIVE MY ZOMBIE)
  (FLAGS PERSONBIT NARTICLEBIT)>

<ROOM FRONT-PORCH
    (DESC "Front Porch")
    (LDESC "You can go south to the front yard, or in to the living room.")
    (IN ROOMS)
    (SOUTH TO FRONT-YARD)
    (FLAGS LIGHTBIT)
    (GLOBAL FRONT-DOOR)
    (IN TO LIVING-ROOM IF FRONT-DOOR IS OPEN)>

<OBJECT FRONT-DOOR
    (IN LOCAL-GLOBALS)
    (DESC "front door")
    (SYNONYM DOOR)
    (ADJECTIVE FRONT)
    (FLAGS OPENABLEBIT DOORBIT OPENBIT)>

<ROOM LIVING-ROOM
    (DESC "Living Room")
    (LDESC "You can go out to the front porch.")
    (IN ROOMS)
    (OUT TO FRONT-PORCH IF FRONT-DOOR IS OPEN)
    (WEST TO KITCHEN)
    (FLAGS LIGHTBIT)
    (GLOBAL FRONT-DOOR)>

<ROOM KITCHEN
  (DESC "Kitchen")
  (LDESC "Your dad's kitchen is even smaller than yours.||You can go east to the
living room or north to the dining room.")
  (IN ROOMS)
  (EAST TO LIVING-ROOM)
  (NORTH TO DINING-ROOM)
  (FLAGS LIGHTBIT)>

<OBJECT FRIDGE
  (SYNONYM FRIDGE REFRIG REFRIGERATOR ICEBOX)
  (DESC "fridge")
  (IN KITCHEN)
  (LDESC "The old refrigerator hums in the corner.")
  (FLAGS CONTBIT OPENABLEBIT)>

<OBJECT BEER
  (IN FRIDGE)
  (SYNONYM CAN SQUIFFY BEER)
  (ADJECTIVE CAN SQUIFFY COLD)
  (DESC "can of Squiffy beer")
  (FLAGS TAKEBIT OPENABLEBIT)
  (ACTION BEER-R)>

<ROUTINE BEER-R ()
  <COND
    (<VERB? LOOK>
      <TELL "A">
      <COND
        (<FSET? ,PRSO ,OPENBIT>
          <TELL "n open">)
        (ELSE
          <TELL "closed">)>
      <TELL " can of Squiffy beer." CR>)
    (<VERB? DRINK>
      <TELL "You don't look old enough." CR>)
    (<AND <VERB? TAKE> <NOT <FSET? ,BEER ,TOUCHBIT>>>
      <TELL "As you take the beer, you hear a strange sound from outside. (Probably unrelated.)" CR>
      <MOVE ,BEER ,PLAYER>
      <MOVE ,ZOMBIE ,CEMETERY>
      <QUEUE I-ZOMBIE -1>
      <FSET ,BEER ,TOUCHBIT>)>>

<ROOM DINING-ROOM
  (IN ROOMS)
  (DESC "Dining Room")
  (SOUTH TO KITCHEN)
  (FLAGS LIGHTBIT)>

<OBJECT TABLE
  (IN DINING-ROOM)
  (DESC "table")
  (LDESC "The dining room table takes up the majority of the room.")
  (SYNONYM TABLE)
  (ADJECTIVE DINING ROOM)
  (FLAGS SURFACEBIT CONTBIT OPENBIT)
  (ACTION TABLE-R)>

<ROUTINE TABLE-R (RARG)
   <COND
    (<VERB? CLIMB>
      <TELL "It wouldn't be able to support you." CR>)>>

<OBJECT MANUAL
  (IN TABLE)
  (DESC "game manual")
  (FLAGS TAKEBIT READBIT OPENABLEBIT)
  (SYNONYM MANUAL GUIDE BOOK)
  (ADJECTIVE GAME)
  (ACTION MANUAL-R)>


<ROUTINE MANUAL-R ()
  <COND
    (<VERB? EXAMINE>
      <TELL "It's the manual that I ">
      <ITALICIZE "know ">
      <TELL "you read before you started this game." CR>)
    (<VERB? READ>
      <COND
        (<FSET? ,MANUAL ,OPENBIT>
          <TELL "It's too late to try to read it now, Watson. The game is on!">)
        (ELSE
          <TELL "It isn't open, and there's only a picture of a rotten hand on the cover." CR>)>)>>

<ROOM WOODS
    (DESC "Woods")
    (LDESC "You can go north, east, or west.")
    (IN ROOMS)
    (NORTH TO FRONT-YARD)
    (EAST TO CEMETERY)
    (WEST TO WOODS-FOUR)
    (FLAGS LIGHTBIT)>


<ROOM WOODS-TWO
    (DESC "Woods")
    (LDESC "You can go north or west.")
    (IN ROOMS)
    (NORTH TO WOODS-THREE)
    (WEST TO CEMETERY)
    (FLAGS LIGHTBIT)>


<ROOM FRONT-YARD
    (DESC "Front Yard")
    (LDESC "You can go north to the porch, west to driveway, southeast to the
cemetery, or any other direction into the woods.")
    (IN ROOMS)
    (NORTH TO FRONT-PORCH)
    (SOUTH TO WOODS)
    (EAST TO WOODS-THREE)
    (WEST TO DRIVEWAY)
    (SOUTHEAST TO CEMETERY)
    (SOUTHWEST TO WOODS-FOUR)
    (FLAGS LIGHTBIT)>


<ROOM DRIVEWAY
    (DESC "Driveway")
    (LDESC "You can go west to the highway, east to the front yard, or south towards the woods.")
    (IN ROOMS)
    (SOUTH TO WOODS-FOUR)
    (EAST TO FRONT-YARD)
    (WEST TO HIGHWAY)
    (FLAGS LIGHTBIT)>

<OBJECT ZIL
    (IN DRIVEWAY)
    (DESC "Zil")
    (LDESC "The old Zil is parked here.")
    (SYNONYM ZIL)
    (FLAGS CONTBIT OPENABLEBIT LOCKEDBIT VEHBIT)
    (ACTION ZIL-R)>

<ROUTINE ZIL-R ()
  <COND
    (<VERB? EXAMINE>
      <TELL
"It's your father's 1977 ZIL-114, with a 6,959 cc V8 engine, the three-speed automatic transmission,
and a big bumper sticker that reads 'VEHBIT.' He had it imported from Russia back in the nineties,
but the VEHBIT kit was added recently." CR>)
    (<VERB? TAKE>
      <TELL "Yeah right!" CR>)>>

<ROOM WOODS-THREE
    (DESC "Woods")
    (LDESC "You can go south, west, or southwest.")
    (IN ROOMS)
    (SOUTH TO WOODS-TWO)
    (WEST TO FRONT-YARD)
    (SOUTHWEST TO CEMETERY)
    (FLAGS LIGHTBIT)>


<ROOM WOODS-FOUR
    (DESC "Woods")
    (LDESC "You can go north, east, or northeast.")
    (IN ROOMS)
    (NORTH TO DRIVEWAY)
    (EAST TO WOODS)
    (NORTHEAST TO FRONT-YARD)
    (FLAGS LIGHTBIT)>


<ROOM HIGHWAY
    (DESC "Highway")
    (LDESC "You can go east towards the driveway. (The road is under construction.)")
    (IN ROOMS)
    (EAST TO DRIVEWAY)
    (FLAGS LIGHTBIT)>
    
<ROOM CABIN
  (DESC "Cabin")
  (IN ROOMS)>

<ROOM SHED
  (DESC "Shed")
  (IN ROOMS)>


;" NEW COMMANDS "

<SYNTAX SPEAK TO OBJECT = V-SPEAK>
<SYNTAX SPEAK WITH OBJECT = V-SPEAK>

<VERB-SYNONYM SPEAK TALK>

<ROUTINE V-SPEAK ()
  <COND
    (<OR <FSET? ,PRSO ,PERSONBIT> <FSET? ,PRSO ,FEMALEBIT>>
      <TELL D, PRSO " does not reply." CR>)
    (T
      <POINTLESS "Trying to speak with" >)>>

<OBJECT TRACTOR
  (IN FRONT-YARD)
  (SYNONYM TRACTOR)
  (DESC "tractor")
  (ACTION TRACTOR-R)
  (FLAGS LIGHTBIT CONTBIT OPENBIT SURFACEBIT)>
  
<ROUTINE TRACTOR-R (RARG)
  <COND
    (<==? .RARG ,M-BEG>
      <COND
        (<VERB? WALK>
          <SETG WINNER ,TRACTOR>
          <V-WALK>
          <SETG WINNER ,PLAYER>)>)
    (<VERB? ENTER>
      <MOVE ,PLAYER ,TRACTOR>)>>

<ROUTINE V-INVENTORY ()
    ;"check for light first"
    <COND (,HERE-LIT
           <COND (<FIRST? ,WINNER>
                  <TELL "You have:" CR>
                  <MAP-CONTENTS (I ,WINNER)
                      <TELL "   " A .I>
                      <AND <FSET? .I ,WORNBIT> <TELL " (worn)">>
                      <COND
                        (<==? .I ,PANTS>
                          <COND
                            (<FIRST? ,PANTS-POCKET>
                              <TELL CR "   In your pants pocket:" CR>
                              <MAP-CONTENTS (J ,PANTS-POCKET)
                                <TELL "      " A .J>>)>)>
                      <AND <FSET? .I ,LIGHTBIT> <TELL " (providing light)">>
                      <COND (<FSET? .I ,CONTBIT>
                             <COND (<FSET? .I ,OPENABLEBIT>
                                    <COND (<FSET? .I ,OPENBIT> <TELL " (open)">)
                                          (ELSE <TELL " (closed)">)>)>
                             <COND (<SEE-INSIDE? .I> <INV-DESCRIBE-CONTENTS .I>)>)>
                      <CRLF>>)
                 (ELSE
                  <TELL "You are empty-handed." CR>)>)
          (ELSE
           <TELL "It's too dark to see what you're carrying." CR>)>>

<OBJECT PANTS
  (IN PLAYER)
  (SYNONYM PANTS)
  (ADJECTIVE MY PAIR)
  (DESC "your pants")
  (FLAGS WEARBIT WORNBIT NARTICLEBIT TAKEBIT)>

<OBJECT PANTS-POCKET
  (IN GLOBAL-OBJECTS)
  (SYNONYM PANTS POCKET)
  (DESC "pants pocket")
  (FLAGS CONTBIT OPENBIT)>

<OBJECT THING
  (IN PANTS-POCKET)
  (SYNONYM THING YOUR AUNT GAVE YOU)
  (DESC "the thing your aunt gave you")
  (FLAGS NARTICLEBIT TAKEBIT CONTBIT OPENBIT)>